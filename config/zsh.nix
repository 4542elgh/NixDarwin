{ config, pkgs, lib, ... }:

{
  # Copy .p10k.zsh to destination
  home.file."${config.xdg.configHome}/zsh/.p10k.zsh".source = ../dotfiles/p10k/.p10k.zsh;

  programs.zsh = {
    enable = true;

    autocd = true;

    # Nix already provide most basic plugins via zsh properties
    enableCompletion = true;
    autosuggestion.enable = true;
    syntaxHighlighting.enable = true;

    # Define dotfile path
    dotDir = "${config.xdg.configHome}/zsh";

    shellAliases = {
      # Nix rebuild and update source pkgs
      nxr = "sudo darwin-rebuild switch --flake ${config.xdg.configHome}/nix";
      nxu = "sudo nix flake update --flake ${config.xdg.configHome}/nix";

      # If you not using vim then WHAT ARE YOU DOING WITH YOUR LIFE???
      vim = "nvim";
      vimrc = "vim ${config.xdg.configHome}/nvim/init.lua";

      # Start a server in this directory using python simple http server.
      serve = "python3 -m http.server --directory .";

      # CLI power up
      ls = "lsd -lah";
      top = "gotop";
      cat = "bat";
      grep = "rg --ignore-case";

      # 10x engineer experience
      fzf = "find . | grep -Ev '.git|node_modules' | fzf --preview 'bat --style=numbers --color=always --line-range :500 {}'";
      # ag = "ag --hidden -l | fzf --preview 'bat --style=numbers --color=always --line-range :500 {}'";

      # Git
      gitclear = "git rm -r --cached .";

      # Tmux
      tmuxnew = "tmux new -s";
      tmuxa = "tmux attach-session -t";
      tmuxkill = "tmux kill-session -t";

      # ZSH related files
      zshrc = "vim ${config.xdg.configHome}/nix/config/zshrc.nix";
      zalias = "vim ${config.xdg.configHome}/nix/config/zshrc.nix";
    };

    oh-my-zsh = {
      enable = true;
      plugins = [ "autojump" ];
    };
    
    plugins = [
      # CLI Theme
      {
        name = "powerlevel10k";
        src = pkgs.fetchFromGitHub {
          owner = "romkatv";
          repo = "powerlevel10k";
          rev = "v1.20.0";
          sha256 = "sha256-ES5vJXHjAKw/VHjWs8Au/3R+/aotSbY7PWnWAMzCR8E=";
        };
        # source THIS file
        file = "powerlevel10k.zsh-theme";
      }
    ];

    #What happens at the:
    #     - begining (mkBefore) 
    #     - end (mkAfter) 
    # of the .zshrc file
    # '' before a special symbol like $ will escape such symbol
    initContent = lib.mkMerge [
      # This is generated by powerlevel10k
      (lib.mkBefore ''
        # Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.config/zsh/.zshrc.
        # Initialization code that may require console input (password prompts, [y/n]
        # confirmations, etc.) must go above this block; everything else may go below.
        if [[ -r "''${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-''${(%):-%n}.zsh" ]]; then
          source "''${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-''${(%):-%n}.zsh"
        fi

        # This is to regenerate compinit once every 24h (regen everytime will cause delay when open terminal)
        autoload -Uz compinit
        if [[ -n $HOME/.cache/zsh/zcompdump-$ZSH_VERSION(#qN.mh+24) ]]; then
            compinit -d "$HOME/.cache/zsh/zcompdump-$ZSH_VERSION"
        else
            compinit -C;
        fi;
      '')

      (lib.mkAfter ''
        # Call macchina for screenfetch
        macchina

        # To customize prompt, run `p10k configure` or edit ~/.config/zsh/.p10k.zsh.
        [[ ! -f ~/.config/zsh/.p10k.zsh ]] || source ~/.config/zsh/.p10k.zsh
      '')
    ];
    
    # Environmental Variables
    sessionVariables = {
      # Define zshrc location
      ZDOTDIR = "${config.xdg.configHome}/zsh";

      EDITOR = "nvim";
      # TERMINAL = "iTerm2";
      # TERM = "xterm-256color"; # Tmux

      # XDG related references
      BAT_CONFIG_PATH = "${config.home.homeDirectory}/bat/config";

      # For vim
      KEYTIMEOUT = 1;

      # ZSH related config
      HISTORY_IGNORE = "(history|clear|exit|ls|cd|pwd|exit|cd ..)";
      HISTFILE = "${config.xdg.configHome}/zsh/.zsh_history";
      HISTSIZE = "10000";
      SAVEHIST = "10000";

      # Terminal config
      CLICOLOR = "1";
      CLICOLOR_FORCE = "1";
    };
  };
}
